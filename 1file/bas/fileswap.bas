Imports System
Imports EnvDTE
Imports EnvDTE80
Imports System.Diagnostics

Public Module FileSwap
    ' Function to jump between source and header files, order is as follows
    ' .h -> .impl.h -> .cpp ->.c -> (wrap around)
    '
    Sub SourceHeaderJumper()
        ' splitted file name
        Dim SArr As System.Array
        ' new filename generated by macros
        Dim NewFN As String
        ' file name base (without extention)
        Dim FNBase As String
        ' file extention
        Dim Ext As String
        ' file header implementation extention
        Dim Impl As String
        ' simple counter
        Dim i As Integer

        If DTE.ActiveDocument() Is Nothing Then
            Exit Sub
        End If
        SArr = DTE.ActiveDocument().Name().Split(".")
        ' has file extention?
        If (SArr.GetLength(0) < 2) Then
            Exit Sub
        End If

        ' file name base
        FNBase = DTE.ActiveDocument().Path()
        For i = 0 To SArr.GetLength(0) - 2
            FNBase = FNBase + SArr(i) + "."
        Next

        ' file extention
        Ext = System.Convert.ToString(SArr(SArr.GetLength(0) - 1)).ToLower()
        ' second to end token, needed to detect the .impl. part
        Impl = System.Convert.ToString(SArr(SArr.GetLength(0) - 2)).ToLower()

        ' business logic
        If (Ext = "cpp") Or (Ext = "c") Then
            ' we are currently in a source file
            NewFN = FNBase + "h"
        ElseIf (Ext = "h") And (Impl = "impl") Then
            ' we are currently in a header implementation file

            ' remove impl. from base
            FNBase = FNBase.Substring(0, FNBase.Length() - 1)
            FNBase = FNBase.Substring(0, FNBase.LastIndexOf(".") + 1)
            NewFN = FNBase + "cpp"
        ElseIf (Ext = "h") Then
            ' we are currently in a header file

            ' if a header implementation file exists let use that
            NewFN = FNBase + "impl.h"
            If Not System.IO.File.Exists(NewFN) Then
                NewFN = FNBase + "cpp"
            End If
        End If

        ' woops target file is not there, lets try for the .c file
        If Not System.IO.File.Exists(NewFN) Then
            NewFN = FNBase + "c"
        End If

        ' if you don't want add file to tabs uncomment next line
        'DTE.ActiveDocument().Close()

        ' opens needed file if it exists
        If System.IO.File.Exists(NewFN) Then
            DTE.ItemOperations.OpenFile(NewFN)
        End If
    End Sub
    Sub SwitchAndCompile()
        If Not DTE.ActiveDocument Is Nothing Then
            Dim startDocName = DTE.ActiveDocument.FullName.ToString

            ' Jump till we get to the cpp file
            While Not (DTE.ActiveDocument.FullName.EndsWith(".cpp") Or DTE.ActiveDocument.FullName.EndsWith(".c"))
                SourceHeaderJumper()
            End While

            ' Compile it
            DTE.ExecuteCommand("Build.Compile")

            ' Need to sleep for a tiny bit so that the compile can begin while we're still 
            ' looking at the cpp file. If we don't, the switch back to .h or .impl.h occurs
            ' too quickly and the compile fails.
            System.Threading.Thread.Sleep(100)

            ' Jump back to the original file
            While Not DTE.ActiveDocument.FullName.ToString = startDocName
                SourceHeaderJumper()
            End While
        End If
    End Sub

End Module
